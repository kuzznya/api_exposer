plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id "io.freefair.lombok" version "5.1.0"
    id 'jacoco'
}

group = 'com.github.kuzznya'
version = projectVersion
sourceCompatibility = 11

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom('io.spring.platform:platform-bom:1.0.1.RELEASE') {
            bomProperty 'spring.version', '5.2.7.RELEASE'
        }
    }
}

dependencies {
    implementation 'org.springframework:spring-web'
    implementation 'org.springframework:spring-webmvc'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.11.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.11.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.1'
    implementation 'jakarta.annotation:jakarta.annotation-api:1.3.5'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
//    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.junit.jupiter:junit-jupiter:5.6.2'
    testCompile 'org.hamcrest:hamcrest:2.2'
    testCompile 'org.springframework:spring-test'
    testCompile 'javax.servlet:javax.servlet-api:3.0.1'
    testCompile 'org.skyscreamer:jsonassert:1.5.0'
}

jar.dependsOn(test)

task sourceJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    useGpgCmd()
    sign(publishing.publications)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'API Exposer Core'
                description = 'API bootstrap creator core module'
                packaging = 'jar'
                url = 'https://github.com/kuzznya/exposer/'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/kuzznya/exposer/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'kuzznya'
                        name = 'Ilya Kuznetsov'
                        email = 'kuzznya@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/kuzznya/exposer.git'
                    developerConnection = 'scm:git:ssh://git@github.com:kuzznya/exposer.git'
                    url = 'https://github.com/kuzznya/exposer'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/kuzznya/exposer/issues'
                }
            }

            artifact jar
            artifact sourceJar
            artifact javadocJar
        }
    }

    repositories {
        mavenLocal()
        mavenCentral {
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.enabled true
    }
}

import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.springframework.boot' version '2.3.1.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

group = 'com.github.kuzznya'
version = '1.1.0'
sourceCompatibility = 11

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencyManagement {
    imports {
        mavenBom SpringBootPlugin.BOM_COORDINATES
    }
}

dependencies {
//    api 'com.github.kuzznya:exposer-core:1.1.0'
    api project(':exposer-core')

    api 'org.springframework.boot:spring-boot-starter-web'

    compileOnly 'org.projectlombok:lombok'

    annotationProcessor "org.springframework.boot:spring-boot-autoconfigure-processor"
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    annotationProcessor 'org.projectlombok:lombok'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}

compileJava.dependsOn(processResources)

jar {
    enabled=true
    dependsOn(test)
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    useGpgCmd()
    sign(publishing.publications)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'API Exposer'
                description = 'API bootstrap creator without controllers'
                packaging = 'jar'
                url = 'https://github.com/kuzznya/exposer/'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/kuzznya/exposer/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'kuzznya'
                        name = 'Ilya Kuznetsov'
                        email = 'kuzznya@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/kuzznya/exposer.git'
                    developerConnection = 'scm:git:ssh://git@github.com:kuzznya/exposer.git'
                    url = 'https://github.com/kuzznya/exposer'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/kuzznya/exposer/issues'
                }
            }

            artifact jar
            artifact sourceJar
            artifact javadocJar
        }
    }

    repositories {
        mavenLocal()
        mavenCentral {
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}
